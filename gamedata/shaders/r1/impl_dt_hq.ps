#include "common.h"

struct 	v2p
{
 	half2 	tc0: 		TEXCOORD0;	// base
 	half2 	tc1: 		TEXCOORD1;	// lmap
 	half2 	tc2: 		TEXCOORD2;	// detail
  	half4	c0:		COLOR0;		// hemi, c0.a * 
	half4	c1:	        COLOR1;		// sun,  c1.a + 
};

uniform sampler2D s_dt_r;
uniform sampler2D s_dt_g;
uniform sampler2D s_dt_b;
uniform sampler2D s_dt_a;

uniform sampler2D s_mask;

//////////////////////////////////////////////////////////////////////////////////////////
// Pixel
half4 	main_ps_2_0	( v2p I )	: COLOR
{
	half4	t_base 	= tex2D		(s_base,I.tc0);
	half4	t_mask 	= tex2D		(s_mask,I.tc0);
	
	t_mask	= t_mask / dot (t_mask, 1.0);
	
	half4	t_lmap 	= tex2D		(s_lmap,I.tc1);

	// lighting
	half3 	l_base 	= t_lmap.rgb;			// base light-map
	half3	l_hemi 	= I.c0 * t_base.a;		// hemi is implicitly inside texture
	half3 	l_sun 	= I.c1 * t_lmap.a;		// sun color
	half3	light	= L_ambient + l_base + l_sun + l_hemi;

	// calc D-texture
	half3 	t_dt_r = tex2D	(s_dt_r, I.tc2) * t_mask.r;
	half3 	t_dt_g = tex2D	(s_dt_g, I.tc2) * t_mask.g;
	half3 	t_dt_b = tex2D	(s_dt_b, I.tc2) * t_mask.b;
	half3 	t_dt_a = tex2D	(s_dt_a, I.tc2) * t_mask.a;
	half3 	detail = t_dt_a + t_dt_b + t_dt_g + t_dt_r;
	
	// final-color
	half3	final 	= (light*t_base.rgb*2)*detail*2	;
	
	// out
	return  half4	(final.rgb,1);
}

